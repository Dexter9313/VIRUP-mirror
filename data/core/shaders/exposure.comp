#version 420 core
#extension GL_ARB_compute_shader : enable

layout (local_size_x = LOCAL_SIZE_2D_X, local_size_y = LOCAL_SIZE_2D_Y) in;

// An image to store data into.
layout (rgba32f, binding = 0) readonly uniform image2D dataIn;
layout (rgba32f, binding = 1) writeonly uniform image2D dataOut;

uniform float exposure;
uniform float dynamicrange       = 10000.0;
uniform float screendynamicrange = 255.0;
uniform float purkinje           = 1.0;

// technically tone mapping
void main()
{
	ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
	vec4 result             = imageLoad(dataIn, pixel_coords);
	if(dynamicrange > 1.0)
	{
		// vec3 grayscale = vec3(0.299, 0.587, 0.114); // photopic
		// vec3 grayscale = vec3(0.06706982, 0.4851017, 0.44782848); // scotopic
		// (Purkinje effect)
		vec3 grayscale
		    = vec3(0.03856366, 0.35832596, 0.60311038); // Purkinje alternative
		vec3 grey = vec3(dot(grayscale, result.rgb));
		// "true" luminance
		vec3 luminance = vec3(dot(vec3(0.2126, 0.7152, 0.0722), result.rgb));

		if(purkinje != 0.0)
		{
			result.rgb = mix(
			    grey, result.rgb,
			    clamp(log(luminance / 0.005) / log(5.0 / 0.005), 0.0, 1.0));
		}
		result.rgb *= exposure;
		// b is such that (1/dynamicrange)^b = 1/(2*screendynamicrange) (=>
		// 1/dynamic range value is mapped to half unit of a pixel when raised
		// to power b)
		float b = log(2.0 * screendynamicrange) / log(dynamicrange);
		result.rgb /= dynamicrange;
		result.rgb = pow(result.rgb, vec3(b));
	}

	if(result.r < 0.5 / 255.0 && result.g < 0.5 / 255.0
	   && result.b < 0.5 / 255.0)
	{
		imageStore(dataOut, pixel_coords, result);
		return;
	}

	/*float screenminvalue=0.0/255.0;
	result.rgb *= 1.0 - screenminvalue;
	result.rgb +=screenminvalue;*/

	imageStore(dataOut, pixel_coords, result);
}
